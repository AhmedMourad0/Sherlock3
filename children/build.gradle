apply plugin: "com.android.library"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "kotlin-kapt"
apply plugin: "de.mannodermaus.android-junit5"
apply plugin: "com.squareup.sqldelight"

android {
    compileSdkVersion 30
    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
        multiDexEnabled true
        resValue "string", "local_ip_address", localIpAddress
        resValue "string", "firebase_application_id", sherlockTestApplicationId
        resValue "string", "firebase_api_key", sherlockTestApiKey
        resValue "string", "firebase_project_id", sherlockTestProjectId
        resValue "string", "firebase_database_url", sherlockTestDatabaseUrl
        resValue "string", "firebase_storage_bucket", sherlockTestStorageBucket
        resValue "string", "firebase_gcm_sender_id", sherlockTestGcmSenderId
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
    sourceSets.each {
        it.java.srcDirs += "src/$it.name/kotlin"
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation "com.google.firebase:firebase-core:18.0.0"
    api "com.google.firebase:firebase-storage:19.2.0"
    api "com.google.firebase:firebase-firestore-ktx:22.0.0"

    api "com.google.firebase:firebase-messaging:21.0.0"
    api "com.google.firebase:firebase-analytics:18.0.0"
    api "com.google.firebase:firebase-auth:20.0.0"
    api "com.google.android.gms:play-services-auth:18.1.0"

    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"

    implementation "com.louiscad.splitties:splitties-appctx:$splittiesVersion"

    implementation "com.jakewharton.timber:timber-android:$timberVersion"

    implementation "com.squareup.sqldelight:android-driver:1.4.1"
    implementation "com.squareup.sqldelight:rxjava2-extensions:1.2.1"

    testImplementation "junit:junit:4.13"
    testImplementation "com.squareup.sqldelight:sqlite-driver:1.4.3"
    androidTestImplementation "androidx.test:runner:1.3.0"
    androidTestImplementation "androidx.test:core:1.3.0"
    androidTestImplementation "androidx.test.ext:junit:1.1.2"
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
    androidTestImplementation "com.squareup.okhttp3:okhttp:4.9.0"

    implementation project(":domain")
}

configurations.all {
    resolutionStrategy.eachDependency { details ->
        if (details.requested.group == "androidx.arch.core"
                && details.requested.name.contains("core-runtime")) {
            details.useVersion "2.1.0"
        }
    }
}

sqldelight {
    ChildrenDatabase {
        packageName = "dev.ahmedmourad.sherlock.children.local"
        sourceFolders = ["sqldelight"]
        schemaOutputDirectory = file("src/main/java/dev/ahmedmourad/sherlock/children/local")
        dialect = "sqlite:3.24"
    }
}
